#------------------------
# Project Version
#------------------------

# Retrieve version from header file
set(HSL_VERSION_HEADER_FILE "${ROOT_DIR}/src/hslservice/service/ServiceVersion.h")

file(STRINGS ${HSL_VERSION_HEADER_FILE} HSL_VERSION_PARTS
  REGEX "^#define[ \t]+HSL_SERVICE_VERSION_(PRODUCT|MAJOR|PHASE|MINOR|RELEASE|HOTFIX)[ \t]+[0-9a-z]+$")
  
# Verify version parts
string(REGEX MATCH "HSL_SERVICE_VERSION_PRODUCT[ \t]+[0-9a-z]+" HSL_VERSION_PRODUCT  ${HSL_VERSION_PARTS})
string(REGEX MATCH "HSL_SERVICE_VERSION_MAJOR[ \t]+[0-9a-z]+" HSL_VERSION_MAJOR  ${HSL_VERSION_PARTS})
string(REGEX MATCH "HSL_SERVICE_VERSION_MINOR[ \t]+[0-9a-z]+" HSL_VERSION_MINOR  ${HSL_VERSION_PARTS})
string(REGEX MATCH "HSL_SERVICE_VERSION_HOTFIX[ \t]+[0-9a-z]+" HSL_VERSION_HOTFIX  ${HSL_VERSION_PARTS})

if(NOT HSL_VERSION_PRODUCT 
    OR NOT HSL_VERSION_MAJOR 
    OR NOT HSL_VERSION_MINOR 
    OR NOT HSL_VERSION_HOTFIX)
  message(FATAL_ERROR "Unable to retrieve project version from ${HSL_VERSION_HEADER_FILE}")
endif()

# Extract version numbers
string(REGEX REPLACE "HSL_SERVICE_VERSION_PRODUCT[ \t]+([0-9a-z]+)" "\\1" HSL_VERSION_PRODUCT  ${HSL_VERSION_PRODUCT})
string(REGEX REPLACE "HSL_SERVICE_VERSION_MAJOR[ \t]+([0-9a-z]+)" "\\1" HSL_VERSION_MAJOR  ${HSL_VERSION_MAJOR})
string(REGEX REPLACE "HSL_SERVICE_VERSION_MINOR[ \t]+([0-9a-z]+)" "\\1" HSL_VERSION_MINOR  ${HSL_VERSION_MINOR})
string(REGEX REPLACE "HSL_SERVICE_VERSION_HOTFIX[ \t]+([0-9a-z]+)" "\\1" HSL_VERSION_HOTFIX  ${HSL_VERSION_HOTFIX})

set(HSL_VERSION_STRING "${HSL_VERSION_PRODUCT}.${HSL_VERSION_MAJOR}.${HSL_VERSION_MINOR}.${HSL_VERSION_HOTFIX}")

message(STATUS "Project version: ${HSL_VERSION_STRING}")
